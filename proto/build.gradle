apply plugin: 'java-library'
apply plugin: 'com.google.protobuf'
apply plugin: 'idea'

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.6.1"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.14.0'
        }
        lite {
            artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                lite {}
                grpc {
                    option 'lite'
                }
            }
        }

    }
}

idea {
    module {
        // Hint for autocompleting generated proto classes
        // Not using generatedSourceDirs because of
        // https://discuss.gradle.org/t/support-for-intellij-2016/15294/8
        sourceDirs += file("${project(':proto').projectDir}/build/generated/source/proto/main/lite")
        sourceDirs += file("${project(':proto').projectDir}/build/generated/source/proto/main/grpc")
    }
}

configurations.all {
    resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.2'
}

dependencies {
    implementation 'javax.annotation:javax.annotation-api:1.2'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    // For protos
    implementation "io.grpc:grpc-netty:1.14.0"
    implementation 'com.google.protobuf:protobuf-java:3.6.1'
    implementation 'io.grpc:grpc-okhttp:1.14.0'
    implementation 'io.grpc:grpc-stub:1.14.0'
    implementation('io.grpc:grpc-protobuf-lite:1.14.0') {
        exclude module: "protobuf-lite"
    }
}
sourceCompatibility = "1.8"
targetCompatibility = "1.8"
